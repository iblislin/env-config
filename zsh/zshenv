# ==================================
# Zsh Options
# ==================================
setopt appendhistory
setopt autocd
setopt auto_pushd
setopt pushd_minus
setopt pushd_ignore_dups
setopt extended_glob
setopt nomatch
setopt prompt_subst
setopt sh_word_split
unsetopt beep
# ==================================
# Zsh Modules
# ==================================
zmodload zsh/complist
autoload -U colors
colors
# ==================================
# Enviroment Variable
# ==================================
path=($HOME/bin $HOME/go/bin $HOME/node/bin /sbin /bin /usr/sbin /usr/bin /usr/games /usr/local/sbin /usr/local/bin)

export ZSH="$HOME/.zsh"

export TERM='screen-256color'

export PAGER=less

export LSCOLORS="GxFxcxdxbxegedabagacad"

export LS_COLORS="di=01;36" # for linux

export ZLS_COLORS="di=01;36:ln=01;35:ex=31:so=32"

local encode=en_US.UTF-8
export LANG=$encode
export LANGUAGE=$encode
export LC_CTYPE=$encode
export LC_COLLATE=$encode
export LC_TIME=$encode
export LC_NUMERIC=$encode
export LC_MONETARY=$encode
export LC_MESSAGES=$encode
export LC_ALL=$encode

export PACKAGEROOT='ftp://ftp.tw.freebsd.org'

# For python shell
export PYTHONSTARTUP=~/.pythonrc

# For vim pager
if [[ -x `where vim | head -1` ]]
then
	if [[ -x $HOME/bin/vimpager ]]
	then
		export PAGER=$HOME/bin/vimpager
		alias less=$PAGER
	fi
	if [[ -x $HOME/bin/vimcat ]]
	then
		alias vcat=$HOME/bin/vimcat
		alias vc=$HOME/bin/vimcat
	fi
fi

# nodejs
export NODE_PATH=$HOME/node/lib/node_modules

# golang
export GOPATH=$HOME/go

# ==================================
# Prompt
# ==================================
export VIRTUAL_ENV_DISABLE_PROMPT=1

function set_prompt {
	local prompt_corner_up="┌─"
	local prompt_corner_down="└─"
	local vi_mode='INSERT'
	local user_color
	local get_git_prompt
	local get_venv_prompt
    local sys_icon=""

    if [ `uname -s` = 'Linux' ]
    then
        sys_icon=""
    elif [ `uname -s` = 'Darwin' ]
    then
        sys_icon=""
    fi

	if [[ $USER == 'root' ]]
	then
		user_color='red'
	else
		user_color='cyan'
	fi

	[ $KEYMAP ] && {
		if [ $KEYMAP = 'vicmd' ]
		then
			vi_mode='NORMAL'
		else
			vi_mode='INSERT'
		fi
	}

	function get_git_prompt() {
		( git symbolic-ref HEAD >& /dev/null ) && {
			echo " %B[%b ${fg_bold[green]}$(git symbolic-ref HEAD | cut -d'/' -f3-)${reset_color}%B]%b"
		}
	}

	function get_venv_prompt() {
		if [ $VIRTUAL_ENV ]
		then
			echo " %B[%bVenv(${fg_bold[magenta]}$(basename $VIRTUAL_ENV)${reset_color})%B]%b"
		fi
	}


	prompt="%B$prompt_corner_up%b%B[${fg_bold[yellow]}%~${reset_color}%B]%b
%B|%b"'$(get_venv_prompt)''$(get_git_prompt)'" %B[-- $vi_mode --]%b
%B$prompt_corner_down%b%B[%F{$user_color}%n%f@%m%(?.. %F{red}Oops%f)]%# %b"
}

function zle-line-init zle-keymap-select {
	set_prompt
	zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select

set_prompt
